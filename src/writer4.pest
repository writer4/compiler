// White space
ws = _{ " " | "\t" }
ws0 = _{ ws* }
ws1 = _{ ws+ }

newline = _{ "\n" | "\r\n" | "\r" }

// Helper
statement_end = _{ newline | EOI }
number_sign = { "#" }
indentation = { ws0 }

// Emphasis
emph_bold = { "**" }
emph_italic = { "__" }
emph_strikethrough = { "~~" }
emph = _{ emph_bold | emph_italic | emph_strikethrough }

// Text
text_segment = { (!emph ~ !statement_end ~ ANY)+ }
text = { (!statement_end ~ emph* ~ text_segment?)* }

// Statements
empty_line_statement = { ws0 ~ statement_end }
header_statement = {
    number_sign ~ number_sign? ~ number_sign? ~
    number_sign? ~ number_sign? ~ number_sign? ~
    ws1 ~ text ~ statement_end
}
paragraph_statement = { text ~ statement_end }
list_item_statement = { indentation ~ "-" ~ (ws1 ~ text)? ~ statement_end }

statement = {
    empty_line_statement | header_statement | list_item_statement | paragraph_statement
}

// Document
document = { SOI ~ (!EOI ~ statement)* ~ EOI }